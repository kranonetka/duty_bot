message         = (mention comma? ws)? command
command         = get_duty_date / set_rooms / add_rooms / remove_rooms / show_list / help / notify_today
mention         = lpar member_type id mention_delim mention_alias rpar
member_type     = 'club' / 'id'
id              = digit+
get_duty_date   = 'когда' ws+ int
set_rooms       = int (ws+ int)*
add_rooms       = plus ws* room_set
remove_rooms    = minus ws* room_set
show_list       = 'список'
help            = 'помощь'
notify_today    = 'кто дежурит сегодня'
room_set        = room_subset (separator room_subset)*
room_subset     = room_range / single_room
room_range      = int minus int
single_room     = int gap
separator       = (comma ws*) / ws+
int             = ~r'[1-9]' digit*
digit           = ~r'[0-9]'
ws              = ~r'\s'
mention_alias   = ~r'[^\n\]]+'
comma           = ','
mention_delim   = '|'
lpar            = '['
rpar            = ']'
minus           = '-'
plus            = '+'
gap             = ''
